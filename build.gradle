plugins {
    id "org.springframework.boot" version "2.3.1.RELEASE"
    id "com.google.osdetector" version "1.6.2"
}

subprojects {
    ext {
        javafxVersion = "14.0.1"
        controlsfxVersion = "11.0.1"
        mp3spiVersion = "1.9.5.4"
        jaadVersion = "0.8.5"
        jaudiotaggerVersion = "2.0.3"
    }

    apply plugin: "java-library"
    apply plugin: "io.spring.dependency-management"

    sourceCompatibility = "14"

    repositories {
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
    }
}

project(":sound-service") {
    dependencies {
        api "org.springframework.boot:spring-boot-starter"
        api "org:jaudiotagger:${jaudiotaggerVersion}"
    }
}

project(":sound-app") {
    apply plugin: "org.springframework.boot"
    apply plugin: "com.google.osdetector"

    ext.platform = osdetector.os == "osx" ? "mac" : osdetector.os == "windows" ? "win" : osdetector.os

    dependencies {
        implementation project(":sound-service")
        implementation "org.openjfx:javafx-base:${javafxVersion}:${platform}"
        implementation "org.openjfx:javafx-graphics:${javafxVersion}:${platform}"
        implementation "org.openjfx:javafx-controls:${javafxVersion}:${platform}"
        implementation "org.openjfx:javafx-fxml:${javafxVersion}:${platform}"
        implementation "org.controlsfx:controlsfx:${controlsfxVersion}"
        runtimeOnly "com.googlecode.soundlibs:mp3spi:${mp3spiVersion}"
        runtimeOnly "net.sourceforge.jaadec:jaad:${jaadVersion}"
    }

    bootJar {
        classifier = "${platform}"
    }
}

project(":sound-console") {
    apply plugin: "org.springframework.boot"

    dependencies {
        implementation project(":sound-service")
        runtimeOnly "com.googlecode.soundlibs:mp3spi:${mp3spiVersion}"
        runtimeOnly "net.sourceforge.jaadec:jaad:${jaadVersion}"
    }
}
