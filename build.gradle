plugins {
    id "org.springframework.boot" version "2.3.1.RELEASE" apply false
    id "org.javamodularity.moduleplugin" version "1.7.0" apply false
    id "org.openjfx.javafxplugin" version "0.0.9" apply false
    id "com.github.spotbugs" version "4.4.4"
}

subprojects {
    ext {
        javafxVersion = "14.0.1"
        controlsfxVersion = "11.0.2"
        jaudiotaggerVersion = "2.0.3"
        jaadVersion = "0.8.5"
        mp3spiVersion = "1.9.5.4"
    }

    apply plugin: "java-library"
    apply plugin: "org.javamodularity.moduleplugin"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "checkstyle"
    apply plugin: "com.github.spotbugs"

    repositories {
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
        testCompileOnly "org.projectlombok:lombok"
        testAnnotationProcessor "org.projectlombok:lombok"
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    javadoc {
        options.charSet = "UTF-8"
        options.encoding = "UTF-8"
    }

    checkstyle {
        toolVersion = "8.34"
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
        ignoreFailures = true
    }

    tasks.withType(Checkstyle) {
        exclude "**/module-info.java"
    }

    spotbugs {
        toolVersion = "4.0.6"
        ignoreFailures = true
    }

    spotbugsMain {
        reports {
            html.enabled = true
        }
    }
}

project(":sound-service") {
    dependencies {
        api "org.springframework.boot:spring-boot-starter"
        implementation project(":sound-util")
        implementation "org:jaudiotagger:${jaudiotaggerVersion}"
    }
}

project(":sound-console") {
    apply plugin: "application"

    application {
        mainModule = "jp.gr.java_conf.stardiopside.sound.console"
        mainClass = "jp.gr.java_conf.stardiopside.sound.Console"
    }

    dependencies {
        implementation project(":sound-service")
        implementation project(":sound-util")
        runtimeOnly "net.sourceforge.jaadec:jaad:${jaadVersion}"
        runtimeOnly "com.googlecode.soundlibs:mp3spi:${mp3spiVersion}"
    }
}

project(":sound-console-boot") {
    apply plugin: "org.springframework.boot"

    bootJar {
        mainClassName = "jp.gr.java_conf.stardiopside.sound.Console"
    }

    dependencies {
        runtimeOnly project(":sound-console")
    }
}

project(":sound-app") {
    apply plugin: "org.openjfx.javafxplugin"
    apply plugin: "application"

    application {
        mainModule = "jp.gr.java_conf.stardiopside.sound.app"
        mainClass = "jp.gr.java_conf.stardiopside.sound.App"
    }

    javafx {
        version = javafxVersion
        modules = [ "javafx.controls", "javafx.fxml" ]
    }

    dependencies {
        implementation project(":sound-service")
        implementation "org.controlsfx:controlsfx:${controlsfxVersion}"
        runtimeOnly "net.sourceforge.jaadec:jaad:${jaadVersion}"
        runtimeOnly "com.googlecode.soundlibs:mp3spi:${mp3spiVersion}"
    }
}

project(":sound-app-boot") {
    apply plugin: "org.springframework.boot"

    bootJar {
        mainClassName = "jp.gr.java_conf.stardiopside.sound.App"
    }

    dependencies {
        runtimeOnly project(":sound-app")
    }
}

project(":sound-checker") {
    apply plugin: "org.openjfx.javafxplugin"
    apply plugin: "application"

    application {
        mainModule = "jp.gr.java_conf.stardiopside.sound.checker"
        mainClass = "jp.gr.java_conf.stardiopside.sound.SoundChecker"
    }

    javafx {
        version = javafxVersion
        modules = [ "javafx.controls", "javafx.fxml" ]
    }

    dependencies {
        implementation project(":sound-service")
        implementation project(":sound-util")
        implementation "org.controlsfx:controlsfx:${controlsfxVersion}"
        runtimeOnly "net.sourceforge.jaadec:jaad:${jaadVersion}"
        runtimeOnly "com.googlecode.soundlibs:mp3spi:${mp3spiVersion}"
    }
}

project(":sound-checker-boot") {
    apply plugin: "org.springframework.boot"

    bootJar {
        mainClassName = "jp.gr.java_conf.stardiopside.sound.SoundChecker"
    }

    dependencies {
        runtimeOnly project(":sound-checker")
    }
}
