plugins {
    id "org.springframework.boot" version "2.4.5" apply false
    id "org.openjfx.javafxplugin" version "0.0.9" apply false
    id "com.github.spotbugs" version "4.7.0"
}

subprojects {
    ext {
        javafxVersion = "16"
    }

    apply plugin: "java-library"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "checkstyle"
    apply plugin: "com.github.spotbugs"

    repositories {
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
        dependencies {
            dependency "org.controlsfx:controlsfx:11.1.0"
            dependency "org:jaudiotagger:2.0.3"
            dependency "net.sourceforge.jaadec:jaad:0.8.5"
            dependency "com.googlecode.soundlibs:mp3spi:1.9.5.4"
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
        testCompileOnly "org.projectlombok:lombok"
        testAnnotationProcessor "org.projectlombok:lombok"
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    javadoc {
        options.charSet = "UTF-8"
        options.encoding = "UTF-8"
    }

    checkstyle {
        toolVersion = "8.41.1"
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
        ignoreFailures = true
    }

    tasks.withType(Checkstyle) {
        exclude "**/module-info.java"
    }

    spotbugs {
        toolVersion = "4.2.2"
        ignoreFailures = true
    }

    spotbugsMain {
        reports {
            html.enabled = true
        }
    }
}

project(":sound-service") {
    dependencies {
        api "org.springframework.boot:spring-boot-starter"
        implementation project(":sound-util")
        implementation project(":sound-compatibility")
    }
}

project(":sound-console") {
    apply plugin: "application"

    application {
        mainModule = "jp.gr.java_conf.stardiopside.sound.console"
        mainClass = "jp.gr.java_conf.stardiopside.sound.Console"
    }

    dependencies {
        implementation project(":sound-service")
        implementation project(":sound-util")
        runtimeOnly "net.sourceforge.jaadec:jaad"
        runtimeOnly "com.googlecode.soundlibs:mp3spi"
    }
}

project(":sound-console-boot") {
    apply plugin: "org.springframework.boot"

    bootJar {
        mainClass = "jp.gr.java_conf.stardiopside.sound.Console"
    }

    dependencies {
        runtimeOnly project(":sound-console")
    }
}

project(":sound-app") {
    apply plugin: "org.openjfx.javafxplugin"
    apply plugin: "application"

    application {
        mainModule = "jp.gr.java_conf.stardiopside.sound.app"
        mainClass = "jp.gr.java_conf.stardiopside.sound.App"
    }

    javafx {
        version = javafxVersion
        modules = ["javafx.controls", "javafx.fxml"]
    }

    dependencies {
        implementation project(":sound-service")
        implementation "org.controlsfx:controlsfx"
        runtimeOnly "net.sourceforge.jaadec:jaad"
        runtimeOnly "com.googlecode.soundlibs:mp3spi"
    }
}

project(":sound-app-boot") {
    apply plugin: "org.springframework.boot"

    bootJar {
        mainClass = "jp.gr.java_conf.stardiopside.sound.App"
    }

    dependencies {
        runtimeOnly project(":sound-app")
    }
}

project(":sound-checker") {
    apply plugin: "org.openjfx.javafxplugin"
    apply plugin: "application"

    application {
        mainModule = "jp.gr.java_conf.stardiopside.sound.checker"
        mainClass = "jp.gr.java_conf.stardiopside.sound.SoundChecker"
    }

    javafx {
        version = javafxVersion
        modules = ["javafx.controls", "javafx.fxml"]
    }

    dependencies {
        implementation project(":sound-service")
        implementation project(":sound-util")
        implementation "org.controlsfx:controlsfx"
        runtimeOnly "net.sourceforge.jaadec:jaad"
        runtimeOnly "com.googlecode.soundlibs:mp3spi"
    }
}

project(":sound-checker-boot") {
    apply plugin: "org.springframework.boot"

    bootJar {
        mainClass = "jp.gr.java_conf.stardiopside.sound.SoundChecker"
    }

    dependencies {
        runtimeOnly project(":sound-checker")
    }
}

project(":sound-compatibility") {
    jar {
        manifest {
            attributes("Automatic-Module-Name": "jp.gr.java_conf.stardiopside.sound.compatibility")
        }
    }

    dependencies {
        implementation "org:jaudiotagger"
    }
}
